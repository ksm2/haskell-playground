interface ToString {
  /**
   * Converts this value to a String.
   */
  toString() String
}

/**
 * Copy a chunk of memory.
 *
 * @param destination - The target pointer in memory
 * @param source - The source pointer in memory
 * @param size - The number of bytes to copy
 */
fn __memcpy(destination Int, source Int, size Int) {
  if size >= 8 {
    // Copy 8 bytes
    __storeIntL(destination, __loadIntL(source))
    __memcpy(destination + 8, source + 8, size - 8)
  } else if size != 0 {
    // Copy one byte
    __storeIntXS(destination, __loadIntXS(source))
    __memcpy(destination + 1, source + 1, size - 1)
  }
}

/**
 * Concatenate two strings.
 *
 * @param str1 - The left string
 * @param str2 - The right string
 * @returns Left and right concatenated
 */
fn __concat(str1 String, str2 String) String {
  length1 := __loadInt(str1 as Int)
  length2 := __loadInt(str2 as Int)
  ptr := __allocate(length1 + length2 + 4)
  __storeInt(ptr, length1 + length2)
  __memcpy(ptr + 4, (str1 as Int) + 4, length1)
  __memcpy(ptr + length1 + 4, (str2 as Int) + 4, length2)

  return ptr
}
